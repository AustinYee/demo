plugins {
    id 'maven-publish'
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.8.8'
}

group 'com.yechangqing.demo.grpc'
version '1.0.0'

sourceSets {
    main {
        proto {
            srcDir "../proto"
        }
        java {
            srcDirs 'src/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.28.1'
    implementation 'io.grpc:grpc-protobuf:1.28.1'
    implementation 'io.grpc:grpc-stub:1.28.1'
    implementation 'com.yechangqing:grpc-demo:1.0.0'
    if (JavaVersion.current().isJava9Compatible()) {
        implementation("javax.annotation:javax.annotation-api:1.3.1")
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.28.1'
        }
    }
    generatedFilesBaseDir = "src"

    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava.include()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.yechangqing'
            artifactId = 'grpc-demo'
            version = '1.0.0'
            from components.java
            artifact sourcesJar
        }
    }
}
